# دکمه

اولین مثال، یک شمارنده است که می‌تواند افزایش یا کاهش یابد.

در ادامه، کد برنامه قرار دارد. روی دکمه "ویرایش" کلیک تا در ویرایشگر آنلاین با آن کار کنید. سعی کنید متن یکی از دکمه‌ها را تغییر دهید. **اکنون روی دکمه ویرایش کلیک کنید!**

[ویرایش](https://elm-lang.org/examples/buttons){ .md-button .md-button--primary }

```elm linenums="1"
import Browser
import Html exposing (Html, button, div, text)
import Html.Events exposing (onClick)



-- MAIN


main =
  Browser.sandbox { init = init, update = update, view = view }



-- MODEL

type alias Model = Int

init : Model
init =
  0


-- UPDATE

type Msg = Increment | Decrement

update : Msg -> Model -> Model
update msg model =
  case msg of
    Increment ->
      model + 1

    Decrement ->
      model - 1


-- VIEW

view : Model -> Html Msg
view model =
  div []
    [ button [ onClick Decrement ] [ text "-" ]
    , div [] [ text (String.fromInt model) ]
    , button [ onClick Increment ] [ text "+" ]
    ]
```

اکنون که کمی در کد جستجو کرده‌اید، ممکن است سوالاتی داشته باشید. مقدار `main` چه کاری انجام می‌دهد؟ اجزای مختلف چگونه با هم ترکیب می‌شوند؟ بیایید کد را مرور و درباره آن صحبت کنیم.

!!! note "یادداشت"

	کد اینجا از [برچسب‌گذاری نوع داده](../types/reading_types.md)، [نوع داده مستعار](../types/type_aliases.md) و [نوع داده سفارشی](../types/custom_types.md) استفاده می‌کند. هدف این بخش این است که با معماری Elm آشنا شوید، بنابراین کمی جلوتر به بررسی نوع داده و شیوه خواندن آن می‌پردازیم. اگر درگیر این جنبه‌ها شده‌اید، پیشنهاد می‌کنم که فصل‌های بعدی را مطالعه کنید!

## Main

مقدار `main` در Elm خاص است. این مقدار توصیف می‌کند که چه چیزی روی صفحه نمایش داده شود. در این مورد، قرار است برنامه را با مقدار `init` راه‌اندازی کنیم، تابع `view` همه چیز را روی صفحه نمایش می‌دهد و ورودی کاربر به تابع `update` منتقل می‌شود. این فرآیند را به عنوان توصیف سطح بالا از برنامه در نظر بگیرید.

## Model

مدل‌سازی داده در Elm بسیار مهم است. هدف **مدل‌سازی** این است که تمام جزییات مربوط به برنامه را به عنوان داده ثبت کند.

برای ساخت یک شمارنده، باید یک عدد را که بالا و پایین می‌رود، پیگیری کنیم. این بدان معناست که مدل ما این بار واقعا کوچک است:

```elm
type alias Model = Int
```

فقط به یک مقدار `Int` نیاز داریم تا شمارش فعلی را پیگیری کنیم. می‌توانیم این را در مقدار اولیه‌مان ببینیم:

```elm
init : Model
init =
  0
```

مقدار اولیه صفر است و با فشار دادن دکمه‌های مختلف افزایش یا کاهش می‌یابد.

## View

یک مدل داریم، اما چگونه آن را روی صفحه نمایش دهیم؟ این نقش تابع `view` است:

```elm
view : Model -> Html Msg
view model =
  div []
    [ button [ onClick Decrement ] [ text "-" ]
    , div [] [ text (String.fromInt model) ]
    , button [ onClick Increment ] [ text "+" ]
    ]
```

این تابع مدل را به عنوان یک آرگومان می‌گیرد که خروجی آن HTML است. بنابراین می‌خواهیم یک دکمه کاهش، شمارش فعلی و یک دکمه افزایش را نمایش دهیم.

توجه داشته باشید که برای هر دکمه یک فراخوان `onClick` داریم که این طور خوانده می‌شود: **زمانی که کسی کلیک کرد، یک پیام تولید کن**. بنابراین دکمه افزایش یک پیام `Increment` تولید می‌کند. این پیام چیست و به کجا می‌رود؟ به تابع `update`!

## Update

تابع `update` توصیف می‌کند که مدل ما چگونه در طول زمان تغییر خواهد کرد.

دو پیام را تعریف می‌کنیم که ممکن است دریافت کند:

```elm
type Msg = Increment | Decrement
```

از آنجا، تابع `update` فقط توصیف می‌کند که چه کاری باید انجام دهیم وقتی یکی از این پیام‌ها را دریافت می‌کنیم:

```elm
update : Msg -> Model -> Model
update msg model =
  case msg of
    Increment ->
      model + 1

    Decrement ->
      model - 1
```

اگر پیام `Increment` دریافت شود، مدل را افزایش می‌دهیم. اگر پیام `Decrement` دریافت شود، مدل را کاهش می‌دهیم.

بنابراین هر بار که یک پیام دریافت شود، آن را از طریق تابع `update` اجرا می‌کنیم تا یک مدل جدید به دست آوریم. سپس تابع `view` را فراخوانی می‌کنیم تا ببینیم چگونه مدل جدید را روی صفحه نمایش دهیم. در ادامه، این روند را تکرار می‌کنیم! ورودی کاربر یک پیام تولید می‌کند، مدل را `بروزرسانی` کرده و آن را روی صفحه `نمایش` می‌دهیم.

## مرور کلی {#overview}

اکنون که تمام اجزای یک برنامه Elm را دیده‌اید، ممکن است کمی آسان‌تر باشد که ببینید چگونه آن‌ها در کنار یکدیگر قرار می‌گیرند:

![نمودار معماری Elm](../assets/diagrams/buttons.svg)

Elm با رندر کردن مقدار اولیه روی صفحه شروع می‌کند. از آنجا، وارد این حلقه می‌شوید:

۱. منتظر ورودی کاربر باشید.
۲. یک پیام به تابع `update` ارسال کنید.
۳. یک `Model` جدید تولید کنید.
۴. برای خروجی HTML تابع `view` را فراخوانی کنید.
۵. HTML جدید را روی صفحه نمایش دهید.
۶. گام‌های ۱ تا ۵ را تکرار کنید!

این فرآیند، جوهره معماری Elm است. هر برنامه‌ای که از این به بعد ببینیم، یک تغییر جزیی در این الگوی پایه خواهد بود.

!!! abstract "تمرین"

	یک دکمه برای بازنشانی شمارنده به صفر اضافه کنید:

	۱. یک نوع `Reset` به `Msg` اضافه کنید.
	۲. یک شاخه `Reset` در تابع `update` اضافه کنید.
	۳. یک دکمه در تابع `view` اضافه کنید.

	می‌توانید برنامه را در [ویرایشگر آنلاین](https://elm-lang.org/examples/buttons) امتحان کنید. اگر این کار خوب پیش رفت، سعی کنید یک دکمه دیگر برای افزایش به اندازه ۱۰ واحد اضافه کنید.